import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'signing'
    id 'eclipse'
    id 'jacoco'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.21.0'
    id 'net.nemerosa.versioning' version '2.8.2'
    id 'org.owasp.dependencycheck' version '5.0.0-M2'
}

group = 'net.reini'

defaultTasks 'build'

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

configurations {
    asciidoclet
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'javax.enterprise:cdi-api:2.0'
    compile 'javax.enterprise.concurrent:javax.enterprise.concurrent-api:1.1'
    compile 'org.slf4j:slf4j-api:1.7.26'
    compile 'com.rabbitmq:amqp-client:5.6.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
    
    testCompile 'org.hamcrest:hamcrest:2.+'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.1'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.4.1'
    testCompile 'org.mockito:mockito-core:2.25.1'
    testCompile 'org.mockito:mockito-junit-jupiter:2.25.1'
    
    runtimeOnly 'org.junit.platform:junit-platform-runner:1.4.1'
    runtimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.1'
    runtimeOnly 'ch.qos.logback:logback-classic:1.2.3'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    required { project.hasProperty('signing.password') && !project.version.endsWith('-SNAPSHOT') }
    sign publishing.publications
}

jar {
    manifest {
        attributes 'Implementation-Title': 'RabbitMQ CDI extension',
                   'Implementation-Version': version,
                   'Build-Date': buildDate,
                   'Build-Time': buildTime,
                   'Build-Revision': versioning.info.commit,
                   'Automatic-Module-Name': "net.reini.rabbitmq.cdi"
        metaInf {
            from file('.')
            include 'LICENSE'
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

check.dependsOn jacocoTestReport, dependencyCheckUpdate

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'RabbitMQ JavaEE/JakartaEE CDI enabler'
                description = 'Enables CDI based usage of RabbitMQ within a JavaEE/JakartaEE application.'
                url = 'https://github.com/reinhapa/rabbitmq-cdi'
                scm {
                    connection = 'scm:git://github.com/reinhapa/rabbitmq-cdi.git'
                    developerConnection = 'scm:git://github.com/reinhapa/rabbitmq-cdi.git'
                    url = 'https://github.com/reinhapa/rabbitmq-cdi'
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'reinhapa'
                        name = 'Patrick Reinhart'
                        email = 'patrick@reini.net'
                    }
                    developer {
                        id = 'ohumbel'
                        name = 'Otmar Humbel'
                        email = 'ohumbel@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
            maven {
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}
