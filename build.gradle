import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'signing'
    id 'eclipse'
    id 'jacoco'
    id 'maven-publish'
    id 'org.asciidoctor.jvm.convert' version '2.0-alpha.4'
    id 'net.nemerosa.versioning' version '2.8.2'
}

group = 'net.reini'

defaultTasks 'build'

Date buildTimeAndDate = new Date()
ext {
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
}

configurations {
    asciidoclet
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'javax.enterprise:cdi-api:1.2'
    compile 'javax.enterprise.concurrent:javax.enterprise.concurrent-api:1.0'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.rabbitmq:amqp-client:5.5.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.3.2'
    
    testCompile 'org.hamcrest:hamcrest:2.+'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.3.2'
    testCompile 'org.mockito:mockito-core:2.23.4'
    testCompile 'org.mockito:mockito-junit-jupiter:2.23.4'
    
    runtime 'org.junit.platform:junit-platform-runner:1.3.2'
    runtime 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    runtime 'ch.qos.logback:logback-classic:1.1.3'

    asciidoclet 'org.asciidoctor:asciidoclet:1.5.6'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    required { project.hasProperty('signing.password') && !project.version.endsWith('-SNAPSHOT') }
    sign publishing.publications
}

jar {
    manifest {
        attributes 'Implementation-Title': 'RabbitMQ CDI extension',
                   'Implementation-Version': version,
                   'Build-Date': buildDate,
                   'Build-Time': buildTime,
                   'Build-Revision': versioning.info.commit,
                   'Automatic-Module-Name': "net.reini.rabbitmq.cdi"
        metaInf {
            from file('.')
            include 'LICENSE'
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

javadoc {
//    options.docletpath = configurations.asciidoclet.files.asType(List)
//    options.doclet = 'org.asciidoctor.Asciidoclet'
//    options.addStringOption "-base-dir", "${projectDir}"
//    options.addStringOption "-attribute",
//            "name=${project.name}," +
//            "version=${project.version}," +
//            "title-link=http://github.com/reinhapa/[${project.name} ${project.version}]"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'RabbitMQ JavaEE/JakartaEE CDI enabler'
                description = 'Enables CDI based usage of RabbitMQ within a JavaEE/JakartaEE application.'
                url = 'https://github.com/reinhapa/rabbitmq-cdi'
                scm {
                    connection = 'scm:git://github.com/reinhapa/rabbitmq-cdi.git'
                    developerConnection = 'scm:git://github.com/reinhapa/rabbitmq-cdi.git'
                    url = 'https://github.com/reinhapa/rabbitmq-cdi'
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'reinhapa'
                        name = 'Patrick Reinhart'
                        email = 'patrick@reini.net'
                    }
                    developer {
                        id = 'ohumbel'
                        name = 'Otmar Humbel'
                        email = 'ohumbel@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
            maven {
                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}
